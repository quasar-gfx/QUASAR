layout(local_size_x = THREADS_PER_LOCALGROUP, local_size_y = THREADS_PER_LOCALGROUP, local_size_z = 1) in;

struct QuadMapData {
    vec3 normal;
    float depth;
    vec2 uv;
    uvec2 offset;
    unsigned int size;
    bool flattened;
};

struct QuadMapDataPacked {
    unsigned int normalSpherical;
    float depth;
    unsigned int offsetSizeFlattened;
};

layout(std430, binding = 0) buffer QuadBufferSizes {
    unsigned int numProxies;
    unsigned int numDepthOffsets;
};

layout(std430, binding = 1) readonly buffer NormalSphericalsBufferInput {
    unsigned int normalSphericalsInput[];
};

layout(std430, binding = 2) readonly buffer DepthBufferInput {
    float depthsInput[];
};

layout(std430, binding = 3) readonly buffer OffsetsBufferInput {
    unsigned int offsetSizeFlattenedsInput[];
};

layout(std430, binding = 4) writeonly buffer NormalSphericalsBufferOutput {
    unsigned int normalSphericalsOutput[];
};

layout(std430, binding = 5) writeonly buffer DepthsBufferOutput {
    float depthsOutput[];
};

layout(std430, binding = 6) writeonly buffer OffsetsBufferOutput {
    unsigned int offsetSizeFlattenedsOutput[];
};

const float PI = 3.1415926535897932384626433832795;

uniform vec2 gBufferSize;
uniform vec2 quadMapSize;

unsigned int packNormalToSphericalHalf(vec3 normal) {
    float theta = atan(normal.z, normal.x);
    float phi = acos(normal.y);

    float theta01 = (theta + PI) / (2.0 * PI);
    float phi01 = phi / PI;

    return packHalf2x16(vec2(theta01, phi01));
}

vec3 unpackSphericalToNormalHalf(unsigned int packedNormal) {
    vec2 spherical01 = unpackHalf2x16(packedNormal);

    float theta = spherical01.x * (2.0 * PI) - PI;
    float phi = spherical01.y * PI;

    return vec3(
        sin(phi) * cos(theta),
        cos(phi),
        sin(phi) * sin(theta)
    );
}

QuadMapData loadInputQuadMap(ivec2 pixelCoord) {
    unsigned int index = unsigned int(pixelCoord.y) * unsigned int(quadMapSize.x) + unsigned int(pixelCoord.x);

    QuadMapDataPacked quadMapDataPacked;
    quadMapDataPacked.normalSpherical = normalSphericalsInput[index];
    quadMapDataPacked.depth = depthsInput[index];
    quadMapDataPacked.offsetSizeFlattened = offsetSizeFlattenedsInput[index];

    QuadMapData quadMapData;
    quadMapData.normal = unpackSphericalToNormalHalf(quadMapDataPacked.normalSpherical);
    quadMapData.depth = quadMapDataPacked.depth;

    quadMapData.offset.x = (quadMapDataPacked.offsetSizeFlattened >> 20) & 0xFFFu;
    quadMapData.offset.y = (quadMapDataPacked.offsetSizeFlattened >> 8) & 0xFFFu;

    unsigned int flattenedAndSize = quadMapDataPacked.offsetSizeFlattened & 0xFFu;
    quadMapData.size = (flattenedAndSize >> 1) & 0x7Fu;
    quadMapData.flattened = (flattenedAndSize & 1u) != 0;

    unsigned int halfSize = 1u << (quadMapData.size - 1u); // 2^size / 2
    quadMapData.uv = (vec2(quadMapData.offset) + float(halfSize) * vec2(0.5)) / gBufferSize;

    return quadMapData;
}

void storeOutputQuadMap(in unsigned int index, in QuadMapData quadMapData) {
    QuadMapDataPacked quadMapDataPacked;
    quadMapDataPacked.normalSpherical = packNormalToSphericalHalf(quadMapData.normal);
    quadMapDataPacked.depth = quadMapData.depth;

    unsigned int flattenedAndSize = ((quadMapData.size & 0x7Fu) << 1) | (quadMapData.flattened ? 1u : 0u);
    quadMapDataPacked.offsetSizeFlattened = ((quadMapData.offset.x & 0xFFFu) << 20) |
                                                    ((quadMapData.offset.y & 0xFFFu) << 8) |
                                                        (flattenedAndSize & 0xFFu);
    normalSphericalsOutput[index] = quadMapDataPacked.normalSpherical;
    depthsOutput[index] = quadMapDataPacked.depth;
    offsetSizeFlattenedsOutput[index] = quadMapDataPacked.offsetSizeFlattened;
}

void main() {
    ivec2 quadMapPixelCoords = ivec2(gl_GlobalInvocationID.xy);
    if (quadMapPixelCoords.x >= int(quadMapSize.x) || quadMapPixelCoords.y >= int(quadMapSize.y)) {
        return;
    }

    QuadMapData quadMapData = loadInputQuadMap(quadMapPixelCoords);
    if (quadMapData.size == 0) {
        return;
    }

    unsigned int size = 1 << quadMapData.size;
    bool flattened = quadMapData.flattened;
    if (!flattened) {
        // for (int i = 0; i < size; i++) {
        //     for (int j = 0; j < size; j++) {
        //         atomicAdd(numDepthOffsets, 4);
        //     }
        // }
        atomicAdd(numDepthOffsets, size * size * 4);
    }

    unsigned int index = atomicAdd(numProxies, 1);
    storeOutputQuadMap(index, quadMapData);
}
