layout(local_size_x = THREADS_PER_LOCALGROUP, local_size_y = THREADS_PER_LOCALGROUP, local_size_z = 1) in;

struct QuadMapData {
    vec3 normal;
    float depth;
    vec2 uv;
    ivec2 offset;
    uint size;
    bool flattened;
};

struct QuadMapDataPacked {
    uvec2 normalAndFlattenedAndSize;
    float depth;
    vec2 uv;
    uint offset;
};

layout(std430, binding = 0) buffer SizesBuffer {
    uint numVertices;
    uint numIndices;
    uint numProxies;
    uint numDepthOffsets;
};

layout(std430, binding = 1) readonly buffer normalAndFlattenedAndSizesBufferInput {
    uvec2 normalAndFlattenedAndSizesInput[];
};

layout(std430, binding = 2) readonly buffer depthBufferInput {
    float depthsInput[];
};

layout(std430, binding = 3) readonly buffer uvsBufferInput {
    vec2 uvsInput[];
};

layout(std430, binding = 4) readonly buffer offsetsBufferInput {
    uint offsetsInput[];
};

layout(std430, binding = 5) writeonly buffer normalAndFlattenedAndSizesBufferOutput {
    uvec2 normalAndFlattenedAndSizesOutput[];
};

layout(std430, binding = 6) writeonly buffer depthBufferOutput {
    float depthsOutput[];
};

layout(std430, binding = 7) writeonly buffer uvsBufferOutput {
    vec2 uvsOutput[];
};

layout(std430, binding = 8) writeonly buffer offsetsBufferOutput {
    uint offsetsOutput[];
};

uniform vec2 quadMapSize;

QuadMapData loadQuadMap(ivec2 pixelCoord) {
    uint index = uint(pixelCoord.y) * uint(quadMapSize.x) + uint(pixelCoord.x);
    QuadMapDataPacked quadMapDataPacked;
    quadMapDataPacked.normalAndFlattenedAndSize = normalAndFlattenedAndSizesInput[index];
    quadMapDataPacked.depth = depthsInput[index];
    quadMapDataPacked.uv = uvsInput[index];
    quadMapDataPacked.offset = offsetsInput[index];

    QuadMapData quadMapData;
    quadMapData.normal.xy = unpackHalf2x16(quadMapDataPacked.normalAndFlattenedAndSize.x);
    quadMapData.normal.z = unpackHalf2x16(quadMapDataPacked.normalAndFlattenedAndSize.y & 0xFFFFu).x;

    quadMapData.depth = quadMapDataPacked.depth;

    quadMapData.uv = quadMapDataPacked.uv;

    quadMapData.offset.x = int(quadMapDataPacked.offset & 0xFFFFu);
    quadMapData.offset.y = int((quadMapDataPacked.offset >> 16) & 0xFFFFu);

    uint flattenedAndSize = (quadMapDataPacked.normalAndFlattenedAndSize.y >> 16) & 0xFFFFu;
    quadMapData.flattened = (flattenedAndSize & (1u << 15)) != 0;
    quadMapData.size = flattenedAndSize & 0x7FFFu;

    return quadMapData;
}

void storeInOutputQuadMap(in uint index, in QuadMapData quadMapData) {
    uint flattenedAndSize = ((quadMapData.size & 0x7FFFu) | (quadMapData.flattened ? 1u << 15 : 0u)) & 0xFFFFu;

    QuadMapDataPacked quadMapDataPacked;
    quadMapDataPacked.normalAndFlattenedAndSize.x = packHalf2x16(quadMapData.normal.xy);
    uint packedNormalZ = packHalf2x16(vec2(quadMapData.normal.z, 0)) & 0xFFFFu;
    quadMapDataPacked.normalAndFlattenedAndSize.y = packedNormalZ | (flattenedAndSize << 16);
    quadMapDataPacked.depth = quadMapData.depth;
    quadMapDataPacked.uv = quadMapData.uv;
    quadMapDataPacked.offset = (quadMapData.offset.x & 0xFFFFu) | ((quadMapData.offset.y & 0xFFFFu) << 16);

    normalAndFlattenedAndSizesOutput[index] = quadMapDataPacked.normalAndFlattenedAndSize;
    depthsOutput[index] = quadMapDataPacked.depth;
    uvsOutput[index] = quadMapDataPacked.uv;
    offsetsOutput[index] = quadMapDataPacked.offset;
}

void main() {
    ivec2 quadMapPixelCoords = ivec2(gl_GlobalInvocationID.xy);
    if (quadMapPixelCoords.x >= int(quadMapSize.x) || quadMapPixelCoords.y >= int(quadMapSize.y)) {
        return;
    }

    QuadMapData quadMapData = loadQuadMap(quadMapPixelCoords);
    if (quadMapData.size == 0) {
        return;
    }

    uint size = 1 << quadMapData.size;
    bool flattened = quadMapData.flattened;
    if (!flattened) {
        for (int i = 0; i < size; i++) {
            for (int j = 0; j < size; j++) {
                atomicAdd(numDepthOffsets, 4);
            }
        }
    }

    uint index = atomicAdd(numProxies, 1);
    storeInOutputQuadMap(index, quadMapData);
}
