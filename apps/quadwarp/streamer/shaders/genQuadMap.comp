layout(local_size_x = THREADS_PER_LOCALGROUP, local_size_y = THREADS_PER_LOCALGROUP, local_size_z = 1) in;

precision highp float;
precision highp sampler2D;
precision highp image2D;

layout(binding = 0) uniform sampler2D normalMap;
layout(binding = 1) uniform sampler2D depthMap;

struct QuadMapData {
    vec3 normal;
    float depth;
    vec2 uv;
    ivec2 offset;
    uint size;
    bool flattened;
};

struct Quad {
    vec3 bottomLeft;
    vec3 bottomRight;
    vec3 topLeft;
    vec3 topRight;
};

struct Quad1D {
    float bottomLeft;
    float bottomRight;
    float topLeft;
    float topRight;
};

struct Quad2D {
    vec2 bottomLeft;
    vec2 bottomRight;
    vec2 topLeft;
    vec2 topRight;
};

// a Quad subdivided into 4 smaller Quads
struct MultiQuad {
    vec3 bottomLeft;
    vec3 bottom;
    vec3 bottomRight;
    vec3 left;
    vec3 center;
    vec3 right;
    vec3 topLeft;
    vec3 top;
    vec3 topRight;
};

struct MultiQuad1D {
    float bottomLeft;
    float bottom;
    float bottomRight;
    float left;
    float center;
    float right;
    float topLeft;
    float top;
    float topRight;
};

struct MultiQuad2D {
    vec2 bottomLeft;
    vec2 bottom;
    vec2 bottomRight;
    vec2 left;
    vec2 center;
    vec2 right;
    vec2 topLeft;
    vec2 top;
    vec2 topRight;
};

struct Plane {
    vec3 normal;
    vec3 point;
};

uniform vec2 remoteWindowSize;
uniform vec2 quadMapSize;
uniform vec2 depthBufferSize;

uniform mat4 view;
uniform mat4 projection;
uniform mat4 viewInverse;
uniform mat4 projectionInverse;

uniform float near;
uniform float far;

uniform bool doAverageNormal;
uniform bool doOrientationCorrection;
uniform float distanceThreshold;
uniform float angleThreshold;
uniform float flatThreshold;

uniform bool discardOutOfRangeDepths;

#define MAX_DEPTH 0.9999

const float epsilon = 1e-5;

const float surfelSize = 0.5;
const vec3 origin = vec3(0.0, 0.0, 0.0);
const vec3 forward = vec3(0.0, 0.0, -1.0);
const vec3 up = vec3(0.0, 1.0, 0.0);
const vec3 left = vec3(-1.0, 0.0, 0.0);
const vec3 right = vec3(1.0, 0.0, 0.0);

layout(std430, binding = 0) writeonly buffer QuadMapBuffer {
    QuadMapData quadMap[];
};

layout(std430, binding = 1) buffer BufferSizesBuffer {
    uint numVertices;
    uint numIndices;
    uint numProxies;
    uint numDepthOffsets;
};

layout(r16f, binding = 0) uniform image2D depthOffsetBuffer;

vec3 ndcToView(mat4 projectionInverse, vec2 ndc, float depth) {
    vec4 ndcPos;
    ndcPos.xy = ndc;
    ndcPos.z = depth * 2.0 - 1.0;
    ndcPos.w = 1.0;

    vec4 viewCoord = projectionInverse * ndcPos;
    viewCoord = viewCoord / viewCoord.w;
    return viewCoord.xyz;
}

vec3 viewToNDC(mat4 projection, vec3 viewCoord) {
    vec4 ndcCoord = projection * vec4(viewCoord, 1.0);
    ndcCoord = ndcCoord / ndcCoord.w;
    ndcCoord.z = (ndcCoord.z + 1.0) / 2.0;
    return ndcCoord.xyz;
}

vec3 viewToWorld(mat4 viewInverse, vec3 viewCoord) {
    vec4 worldCoord = viewInverse * vec4(viewCoord, 1.0);
    worldCoord = worldCoord / worldCoord.w;
    return worldCoord.xyz;
}

vec3 worldToView(mat4 view, vec3 worldCoord) {
    vec4 viewCoord = view * vec4(worldCoord, 1.0);
    viewCoord = viewCoord / viewCoord.w;
    return viewCoord.xyz;
}

vec2 viewToScreen(mat4 projection, vec3 viewCoord) {
    vec2 ndc = viewToNDC(projection, viewCoord).xy;
    vec2 uv = (ndc + 1.0) / 2.0;
    return uv;
}

float signedDistance(vec3 p1, vec3 p2) {
    float dist = distance(p1, p2);
    return (length(p1) > length(p2)) ? dist : -dist;
}

float sampleDepth(sampler2D depthMap, vec2 sampleCoord) {
    float depth = texture(depthMap, sampleCoord).x;
    depth = clamp(depth, 0.0, MAX_DEPTH);
    return depth;
}

float normalizeDepth(float depth) {
    depth = clamp(depth, near, far);
    float z = (depth - near) / (far - near);
    return (1.0 - (1.0 / (1.0 + z * (far - near) / near)));
}

vec3 sampleNormal(sampler2D normalMap, vec2 sampleCoord) {
    return normalize(texture(normalMap, sampleCoord).xyz);
}

vec3 normalToView(mat4 view, vec3 normalWorld) {
    // skybox normals might be NaN, just set normal to face the camera
    if (any(isnan(normalWorld))) {
        return -forward;
    }

    // extract the rotation part of the view matrix
    mat3 rotationMatrix = mat3(view);
    return normalize(rotationMatrix * normalWorld);
}

vec3 averageNormals(MultiQuad pixelNormalsView, MultiQuad pixelCoordsView) {
    vec3 normal = pixelNormalsView.center;
    float n = 1;
    if (normalizeDepth(distance(pixelCoordsView.center, pixelCoordsView.bottomLeft)) < distanceThreshold) {
        normal += pixelNormalsView.bottomLeft; n++;
    }
    if (normalizeDepth(distance(pixelCoordsView.center, pixelCoordsView.bottom)) < distanceThreshold) {
        normal += pixelNormalsView.bottom; n++;
    }
    if (normalizeDepth(distance(pixelCoordsView.center, pixelCoordsView.bottomRight)) < distanceThreshold) {
        normal += pixelNormalsView.bottomRight; n++;
    }
    if (normalizeDepth(distance(pixelCoordsView.center, pixelCoordsView.left)) < distanceThreshold) {
        normal += pixelNormalsView.left; n++;
    }
    if (normalizeDepth(distance(pixelCoordsView.center, pixelCoordsView.right)) < distanceThreshold) {
        normal += pixelNormalsView.right; n++;
    }
    if (normalizeDepth(distance(pixelCoordsView.center, pixelCoordsView.topLeft)) < distanceThreshold) {
        normal += pixelNormalsView.topLeft; n++;
    }
    if (normalizeDepth(distance(pixelCoordsView.center, pixelCoordsView.top)) < distanceThreshold) {
        normal += pixelNormalsView.top; n++;
    }
    if (normalizeDepth(distance(pixelCoordsView.center, pixelCoordsView.topRight)) < distanceThreshold) {
        normal += pixelNormalsView.topRight; n++;
    }
    normal /= n;
    normal = normalize(normal);
    return normal;
}

vec3 constrainToMaxAngle(vec3 vectorNormalized, vec3 axisNormalized, float maxAngle) {
    float dotProduct = dot(vectorNormalized, axisNormalized);
    float angle = acos(dotProduct);
    if (angle > maxAngle) {
        vec3 rotationAxis = normalize(cross(vectorNormalized, axisNormalized));
        float cosMaxAngle = cos(maxAngle);
        float sinMaxAngle = sin(maxAngle);
        vec3 crossProduct = cross(rotationAxis, vectorNormalized);
        vectorNormalized = vectorNormalized * cosMaxAngle + \
                           cross(rotationAxis, vectorNormalized) * sinMaxAngle + \
                           rotationAxis * dot(rotationAxis, vectorNormalized) * (1.0 - cosMaxAngle);
    }

    return normalize(vectorNormalized);
}

vec3 rayPlaneIntersection(vec3 rayOrigin, vec3 rayDirection, Plane plane) {
    float denominator = dot(rayDirection, plane.normal);
    if (abs(denominator) < epsilon) {
        return vec3(1.0/0.0);
    }

    float t = dot(plane.point - rayOrigin, plane.normal) / denominator;
    if (t < 0.0) {
        return vec3(1.0/0.0);
    }

    vec3 intersection = rayOrigin + t * rayDirection;
    return intersection;
}

vec3 pointPlaneIntersection(vec3 pt, Plane plane) {
    vec3 rayDirection = normalize(pt);
    return rayPlaneIntersection(origin, rayDirection, plane);
}

vec3 findQuadPointCorner(
    vec2 surfelNDC,
    vec3 surfelViewPlane,
    vec3 surfelViewPlanePixelNeighbor1,
    vec3 surfelViewPlanePixelNeighbor2,
    vec3 surfelViewPlanePixelNeighbor3,
    float pixelDepthCenter, float pixelDepthNeighbor1, float pixelDepthNeighbor2, float pixelDepthNeighbor3)
{
    // if (surfelNDC.x <= -1.0 || surfelNDC.x >= 1.0 ||
    //     surfelNDC.y <= -1.0 || surfelNDC.y >= 1.0) {
    //     return surfelViewPlane;
    // }

    if (pixelDepthCenter >= MAX_DEPTH || pixelDepthNeighbor1 >= MAX_DEPTH || pixelDepthNeighbor2 >= MAX_DEPTH || pixelDepthNeighbor3 >= MAX_DEPTH) {
        return surfelViewPlane;
    }

    // find distance of depth of point on this pixel's plane to the same point on neighboring pixels' planes
    float dist1 = normalizeDepth(distance(surfelViewPlane, surfelViewPlanePixelNeighbor1));
    float dist2 = normalizeDepth(distance(surfelViewPlane, surfelViewPlanePixelNeighbor2));
    float dist3 = normalizeDepth(distance(surfelViewPlane, surfelViewPlanePixelNeighbor3));

    // add the contribution of each neighboring pixel to interpolated depth, if it is within distanceThreshold
    float depth = pixelDepthCenter;
    float n = 1;
    if (dist1 < distanceThreshold) {
        depth += pixelDepthNeighbor1;
        n++;
    }
    if (dist2 < distanceThreshold) {
        depth += pixelDepthNeighbor2;
        n++;
    }
    if (dist3 < distanceThreshold) {
        depth += pixelDepthNeighbor3;
        n++;
    }
    depth /= n;

    // only return the interpolated point if all neighboring pixels are within distanceThreshold
    vec3 surfelViewBottomLeftInterp = ndcToView(projectionInverse, surfelNDC, depth);
    if (n >= 3) {
        return surfelViewBottomLeftInterp;
    }
    return surfelViewPlane;
}

vec3 findQuadPointEdge(
    vec2 surfelNDC,
    vec3 surfelViewPlane,
    vec3 surfelViewPlanePixelNeighbor,
    float pixelDepthCenter, float pixelDepthNeighbor)
{
    // if (surfelNDC.x <= -1.0 || surfelNDC.x >= 1.0 ||
    //     surfelNDC.y <= -1.0 || surfelNDC.y >= 1.0) {
    //     return surfelViewPlane;
    // }

    if (pixelDepthCenter >= MAX_DEPTH || pixelDepthNeighbor >= MAX_DEPTH) {
        return surfelViewPlane;
    }

    // return interpolated point if neighboring pixel's depth is within distanceThreshold
    if (normalizeDepth(distance(surfelViewPlane, surfelViewPlanePixelNeighbor)) < distanceThreshold) {
        float depth = (pixelDepthCenter + pixelDepthNeighbor) / 2.0;
        return ndcToView(projectionInverse, surfelNDC, depth);
    }
    return surfelViewPlane;
}

float distanceAlongAxis(vec3 a, vec3 b, vec3 axis) {
    vec3 aProj = dot(a, axis) * axis;
    vec3 bProj = dot(b, axis) * axis;
    return distance(aProj, bProj);
}

bool quadHasExtremeOrientation(in MultiQuad quad, vec3 frustumNormalView) {
    float avgDist = 0.0;
    avgDist += distanceAlongAxis(quad.center, quad.bottomLeft, frustumNormalView);
    avgDist += distanceAlongAxis(quad.center, quad.bottom, frustumNormalView);
    avgDist += distanceAlongAxis(quad.center, quad.bottomRight, frustumNormalView);
    avgDist += distanceAlongAxis(quad.center, quad.left, frustumNormalView);
    avgDist += distanceAlongAxis(quad.center, quad.right, frustumNormalView);
    avgDist += distanceAlongAxis(quad.center, quad.topLeft, frustumNormalView);
    avgDist += distanceAlongAxis(quad.center, quad.top, frustumNormalView);
    avgDist += distanceAlongAxis(quad.center, quad.topRight, frustumNormalView);
    avgDist /= 8.0;
    if (normalizeDepth(avgDist) > distanceThreshold) {
        return true;
    }
    return false;
}

void createSurfels(
    out MultiQuad surfelView,
    in MultiQuad2D surfelNDCs, in MultiQuad surfelViewNearPlane, in MultiQuad pixelCoordsView, in MultiQuad pixelNormalsView, in MultiQuad1D pixelDepths
) {
    // create planes for each pixel
    Plane planeBottomLeft  = Plane(pixelNormalsView.bottomLeft, pixelCoordsView.bottomLeft);
    Plane planeBottom      = Plane(pixelNormalsView.bottom, pixelCoordsView.bottom);
    Plane planeBottomRight = Plane(pixelNormalsView.bottomRight, pixelCoordsView.bottomRight);
    Plane planeLeft        = Plane(pixelNormalsView.left, pixelCoordsView.left);
    Plane planeCenter      = Plane(pixelNormalsView.center, pixelCoordsView.center);
    Plane planeRight       = Plane(pixelNormalsView.right, pixelCoordsView.right);
    Plane planeTopLeft     = Plane(pixelNormalsView.topLeft, pixelCoordsView.topLeft);
    Plane planeTop         = Plane(pixelNormalsView.top, pixelCoordsView.top);
    Plane planeTopRight    = Plane(pixelNormalsView.topRight, pixelCoordsView.topRight);

    // find all points on a ray going through each coordinate intersecting a plane defined by neighboring pixels
    vec3 surfelViewBottomLeftPlane                = pointPlaneIntersection(surfelViewNearPlane.bottomLeft, planeCenter);
    vec3 surfelViewBottomLeftPlanePixelBottomLeft = pointPlaneIntersection(surfelViewNearPlane.bottomLeft, planeBottomLeft);
    vec3 surfelViewBottomLeftPlanePixelBottom     = pointPlaneIntersection(surfelViewNearPlane.bottomLeft, planeBottom);
    vec3 surfelViewBottomLeftPlanePixelLeft       = pointPlaneIntersection(surfelViewNearPlane.bottomLeft, planeLeft);

    vec3 surfelViewBottomPlane            = pointPlaneIntersection(surfelViewNearPlane.bottom, planeCenter);
    vec3 surfelViewBottomPlanePixelBottom = pointPlaneIntersection(surfelViewNearPlane.bottom, planeBottom);

    vec3 surfelViewBottomRightPlane                 = pointPlaneIntersection(surfelViewNearPlane.bottomRight, planeCenter);
    vec3 surfelViewBottomRightPlanePixelBottomRight = pointPlaneIntersection(surfelViewNearPlane.bottomRight, planeBottomRight);
    vec3 surfelViewBottomRightPlanePixelBottom      = pointPlaneIntersection(surfelViewNearPlane.bottomRight, planeBottom);
    vec3 surfelViewBottomRightPlanePixelRight       = pointPlaneIntersection(surfelViewNearPlane.bottomRight, planeRight);

    vec3 surfelViewLeftPlane          = pointPlaneIntersection(surfelViewNearPlane.left, planeCenter);
    vec3 surfelViewLeftPlanePixelLeft = pointPlaneIntersection(surfelViewNearPlane.left, planeLeft);

    vec3 surfelViewRightPlane           = pointPlaneIntersection(surfelViewNearPlane.right, planeCenter);
    vec3 surfelViewRightPlanePixelRight = pointPlaneIntersection(surfelViewNearPlane.right, planeRight);

    vec3 surfelViewTopLeftPlane             = pointPlaneIntersection(surfelViewNearPlane.topLeft, planeCenter);
    vec3 surfelViewTopLeftPlanePixelTopLeft = pointPlaneIntersection(surfelViewNearPlane.topLeft, planeTopLeft);
    vec3 surfelViewTopLeftPlanePixelTop     = pointPlaneIntersection(surfelViewNearPlane.topLeft, planeTop);
    vec3 surfelViewTopLeftPlanePixelLeft    = pointPlaneIntersection(surfelViewNearPlane.topLeft, planeLeft);

    vec3 surfelViewTopPlane         = pointPlaneIntersection(surfelViewNearPlane.top, planeCenter);
    vec3 surfelViewTopPlanePixelTop = pointPlaneIntersection(surfelViewNearPlane.top, planeTop);

    vec3 surfelViewTopRightPlane              = pointPlaneIntersection(surfelViewNearPlane.topRight, planeCenter);
    vec3 surfelViewTopRightPlanePixelTopRight = pointPlaneIntersection(surfelViewNearPlane.topRight, planeTopRight);
    vec3 surfelViewTopRightPlanePixelTop      = pointPlaneIntersection(surfelViewNearPlane.topRight, planeTop);
    vec3 surfelViewTopRightPlanePixelRight    = pointPlaneIntersection(surfelViewNearPlane.topRight, planeRight);

    /**********************************

      (ptl)  ----   (pt) ----  (ptr)
        |            |           |
        |  - stl -  st  -  str - |
        |            |           |
     (pl)  - sl - (pc/sc) - sr - (pr)
        |            |           |
        |  - sbl -  sb  -  sbr - |
        |            |           |
      (pbl)  ----   (pb) ----  (pbr)

    **********************************/

    // adjust points
    vec3 surfelViewBottomLeft = findQuadPointCorner(
        surfelNDCs.bottomLeft,
        surfelViewBottomLeftPlane,
        surfelViewBottomLeftPlanePixelBottomLeft, surfelViewBottomLeftPlanePixelBottom, surfelViewBottomLeftPlanePixelLeft,
        pixelDepths.center, pixelDepths.bottomLeft, pixelDepths.bottom, pixelDepths.left
    );

    vec3 surfelViewBottom = findQuadPointEdge(
        surfelNDCs.bottom,
        surfelViewBottomPlane,
        surfelViewBottomPlanePixelBottom,
        pixelDepths.center, pixelDepths.bottom
    );

    vec3 surfelViewBottomRight = findQuadPointCorner(
        surfelNDCs.bottomRight,
        surfelViewBottomRightPlane,
        surfelViewBottomRightPlanePixelBottomRight, surfelViewBottomRightPlanePixelBottom, surfelViewBottomRightPlanePixelRight,
        pixelDepths.center, pixelDepths.bottomRight, pixelDepths.bottom, pixelDepths.right
    );

    vec3 surfelViewLeft = findQuadPointEdge(
        surfelNDCs.left,
        surfelViewLeftPlane,
        surfelViewLeftPlanePixelLeft,
        pixelDepths.center, pixelDepths.left
    );

    vec3 surfelViewCenter = pixelCoordsView.center;

    vec3 surfelViewRight = findQuadPointEdge(
        surfelNDCs.right,
        surfelViewRightPlane,
        surfelViewRightPlanePixelRight,
        pixelDepths.center, pixelDepths.right
    );

    vec3 surfelViewTopLeft = findQuadPointCorner(
        surfelNDCs.topLeft,
        surfelViewTopLeftPlane,
        surfelViewTopLeftPlanePixelTopLeft, surfelViewTopLeftPlanePixelTop, surfelViewTopLeftPlanePixelLeft,
        pixelDepths.center, pixelDepths.topLeft, pixelDepths.top, pixelDepths.left
    );

    vec3 surfelViewTop = findQuadPointEdge(
        surfelNDCs.top,
        surfelViewTopPlane,
        surfelViewTopPlanePixelTop,
        pixelDepths.center, pixelDepths.top
    );

    vec3 surfelViewTopRight = findQuadPointCorner(
        surfelNDCs.topRight,
        surfelViewTopRightPlane,
        surfelViewTopRightPlanePixelTopRight, surfelViewTopRightPlanePixelTop, surfelViewTopRightPlanePixelRight,
        pixelDepths.center, pixelDepths.topRight, pixelDepths.top, pixelDepths.right
    );

    surfelView.bottomLeft  = surfelViewBottomLeft;
    surfelView.bottom      = surfelViewBottom;
    surfelView.bottomRight = surfelViewBottomRight;
    surfelView.left        = surfelViewLeft;
    surfelView.center      = surfelViewCenter;
    surfelView.right       = surfelViewRight;
    surfelView.topLeft     = surfelViewTopLeft;
    surfelView.top         = surfelViewTop;
    surfelView.topRight    = surfelViewTopRight;
}

void storeInQuadMap(in ivec2 pixelCoord, in QuadMapData quadMapData) {
    uint index = uint(pixelCoord.y) * uint(quadMapSize.x) + uint(pixelCoord.x);
    quadMap[index] = quadMapData;
}

void storeInDepthOffsetBuffer(in ivec2 pixelCoord, in float depthOffset) {
    imageStore(depthOffsetBuffer, pixelCoord, vec4(depthOffset, 0.0, 0.0, 0.0));
}

void storeDepthOffsets(in ivec2 pixelCoord, in Quad1D depthOffsets) {
    storeInDepthOffsetBuffer(pixelCoord + ivec2(0, 0), depthOffsets.bottomLeft);
    storeInDepthOffsetBuffer(pixelCoord + ivec2(1, 0), depthOffsets.bottomRight);
    storeInDepthOffsetBuffer(pixelCoord + ivec2(0, 1), depthOffsets.topLeft);
    storeInDepthOffsetBuffer(pixelCoord + ivec2(1, 1), depthOffsets.topRight);
}

Quad1D findDepthOffsets(in Quad quadView, in Plane proxyPlane) {
    Quad quadPtsOnPlane;
    quadPtsOnPlane.bottomLeft  = pointPlaneIntersection(quadView.bottomLeft,  proxyPlane);
    quadPtsOnPlane.bottomRight = pointPlaneIntersection(quadView.bottomRight, proxyPlane);
    quadPtsOnPlane.topLeft     = pointPlaneIntersection(quadView.topLeft,     proxyPlane);
    quadPtsOnPlane.topRight    = pointPlaneIntersection(quadView.topRight,    proxyPlane);

    Quad1D depthOffsets;
    depthOffsets.bottomLeft  = signedDistance(quadPtsOnPlane.bottomLeft,  quadView.bottomLeft);
    depthOffsets.bottomRight = signedDistance(quadPtsOnPlane.bottomRight, quadView.bottomRight);
    depthOffsets.topLeft     = signedDistance(quadPtsOnPlane.topLeft,     quadView.topLeft);
    depthOffsets.topRight    = signedDistance(quadPtsOnPlane.topRight,    quadView.topRight);

    return depthOffsets;
}

void storeSurfelsInQuadMap(
    in ivec2 pixelCoord,
    in vec2 quadCenterPixelCoord,
    in vec3 proxyPlaneNormal,
    in Quad surfelBottomLeft, in Quad surfelBottomRight, in Quad surfelTopLeft, in Quad surfelTopRight
) {
    vec3 proxyPlanePt = (surfelBottomLeft.bottomLeft  + surfelBottomLeft.bottomRight  + surfelBottomLeft.topLeft  + surfelBottomLeft.topRight  +
                         surfelBottomRight.bottomLeft + surfelBottomRight.bottomRight + surfelBottomRight.topLeft + surfelBottomRight.topRight +
                         surfelTopLeft.bottomLeft     + surfelTopLeft.bottomRight     + surfelTopLeft.topLeft     + surfelTopLeft.topRight     +
                         surfelTopRight.bottomLeft    + surfelTopRight.bottomRight    + surfelTopRight.topLeft    + surfelTopRight.topRight    ) / 16.0;

    Plane proxyPlane;
    proxyPlane.normal = proxyPlaneNormal;
    proxyPlane.point = proxyPlanePt;

    // find depth offsets for each corner of the quad
    Quad1D depthOffsetsBottomLeft  = findDepthOffsets(surfelBottomLeft, proxyPlane);
    Quad1D depthOffsetsBottomRight = findDepthOffsets(surfelBottomRight, proxyPlane);
    Quad1D depthOffsetsTopLeft     = findDepthOffsets(surfelTopLeft, proxyPlane);
    Quad1D depthOffsetsTopRight    = findDepthOffsets(surfelTopRight, proxyPlane);

    // find 3d point of center of proxy
    vec2 quadCenterUV = quadCenterPixelCoord / remoteWindowSize;
    vec2 quadCenterNDC = quadCenterUV * 2.0 - 1.0;
    vec3 quadCenterNearPlane = ndcToView(projectionInverse, quadCenterNDC, 0.0);

    // project the center of the quad onto the proxy plane to find uv for proxy plane
    vec3 planeCenterPt = pointPlaneIntersection(quadCenterNearPlane, proxyPlane);
    vec3 proxyPlaneNDC = viewToNDC(projection, planeCenterPt);
    vec2 proxyPlaneUV = (proxyPlaneNDC.xy + 1.0) / 2.0;

    float proxyPlaneDepth = min(proxyPlaneNDC.z, MAX_DEPTH);

    // if more than half the surfels are below the threshold, then the plane is considered flattened
    int numBelowThreshold = 0;
    int total = 16;
    if (abs(depthOffsetsBottomLeft.bottomLeft)   < flatThreshold) numBelowThreshold++;
    if (abs(depthOffsetsBottomLeft.bottomRight)  < flatThreshold) numBelowThreshold++;
    if (abs(depthOffsetsBottomLeft.topLeft)      < flatThreshold) numBelowThreshold++;
    if (abs(depthOffsetsBottomLeft.topRight)     < flatThreshold) numBelowThreshold++;

    if (abs(depthOffsetsBottomRight.bottomLeft)  < flatThreshold) numBelowThreshold++;
    if (abs(depthOffsetsBottomRight.bottomRight) < flatThreshold) numBelowThreshold++;
    if (abs(depthOffsetsBottomRight.topLeft)     < flatThreshold) numBelowThreshold++;
    if (abs(depthOffsetsBottomRight.topRight)    < flatThreshold) numBelowThreshold++;

    if (abs(depthOffsetsTopLeft.bottomLeft)      < flatThreshold) numBelowThreshold++;
    if (abs(depthOffsetsTopLeft.bottomRight)     < flatThreshold) numBelowThreshold++;
    if (abs(depthOffsetsTopLeft.topLeft)         < flatThreshold) numBelowThreshold++;
    if (abs(depthOffsetsTopLeft.topRight)        < flatThreshold) numBelowThreshold++;

    if (abs(depthOffsetsTopRight.bottomLeft)     < flatThreshold) numBelowThreshold++;
    if (abs(depthOffsetsTopRight.bottomRight)    < flatThreshold) numBelowThreshold++;
    if (abs(depthOffsetsTopRight.topLeft)        < flatThreshold) numBelowThreshold++;
    if (abs(depthOffsetsTopRight.topRight)       < flatThreshold) numBelowThreshold++;

    bool flattened = numBelowThreshold > total / 2;

    QuadMapData quadMapData;
    quadMapData.flattened = flattened;
    quadMapData.uv = proxyPlaneUV;
    quadMapData.depth = proxyPlaneDepth;
    quadMapData.normal = proxyPlaneNormal;
    quadMapData.offset = pixelCoord;
    quadMapData.size = 2; // 2x2 surfels
    storeInQuadMap(pixelCoord, quadMapData);

    // store empty depth offsets if the proxy plane is flat
    if (quadMapData.flattened) {
        depthOffsetsBottomLeft  = Quad1D(0.0, 0.0, 0.0, 0.0);
        depthOffsetsBottomRight = Quad1D(0.0, 0.0, 0.0, 0.0);
        depthOffsetsTopLeft     = Quad1D(0.0, 0.0, 0.0, 0.0);
        depthOffsetsTopRight    = Quad1D(0.0, 0.0, 0.0, 0.0);
    }

    storeDepthOffsets(4 * pixelCoord + 2 * ivec2(0, 0), depthOffsetsBottomLeft);
    storeDepthOffsets(4 * pixelCoord + 2 * ivec2(1, 0), depthOffsetsBottomRight);
    storeDepthOffsets(4 * pixelCoord + 2 * ivec2(0, 1), depthOffsetsTopLeft);
    storeDepthOffsets(4 * pixelCoord + 2 * ivec2(1, 1), depthOffsetsTopRight);
}

void main() {
    /**********************************

      (ptl)  ----   (pt) ----  (ptr)
        |            |           |
        |  - stl -  st  -  str - |
        |            |           |
     (pl)  - sl - (pc/sc) - sr - (pr)
        |            |           |
        |  - sbl -  sb  -  sbr - |
        |            |           |
      (pbl)  ----   (pb) ----  (pbr)

    **********************************/

    ivec2 pixelCoord = ivec2(gl_GlobalInvocationID.xy);
    if (pixelCoord.x >= int(remoteWindowSize.x) || pixelCoord.y >= int(remoteWindowSize.y)) {
        return;
    }

    // reset counters
    numVertices = 0;
    numIndices = 0;
    numProxies = 0;
    numDepthOffsets = 0;

    // calculate coords (see above)
    vec2 surfelCornerCoordBottomLeft = vec2(gl_GlobalInvocationID.xy);
    vec2 pixelCenterCoord = surfelCornerCoordBottomLeft + vec2(0.5, 0.5);
    vec2 surfelCenterCoord = pixelCenterCoord;

    MultiQuad2D pixelCenterCoords;
    pixelCenterCoords.bottomLeft  = pixelCenterCoord + vec2(-1, -1);
    pixelCenterCoords.bottom      = pixelCenterCoord + vec2(+0, -1);
    pixelCenterCoords.bottomRight = pixelCenterCoord + vec2(+1, -1);
    pixelCenterCoords.left        = pixelCenterCoord + vec2(-1, +0);
    pixelCenterCoords.center      = pixelCenterCoord + vec2(+0, +0);
    pixelCenterCoords.right       = pixelCenterCoord + vec2(+1, +0);
    pixelCenterCoords.topLeft     = pixelCenterCoord + vec2(-1, +1);
    pixelCenterCoords.top         = pixelCenterCoord + vec2(+0, +1);
    pixelCenterCoords.topRight    = pixelCenterCoord + vec2(+1, +1);

    MultiQuad2D surfelCornerCoords;
    surfelCornerCoords.bottomLeft  = surfelCenterCoord + vec2(-0.5, -0.5);
    surfelCornerCoords.bottom      = surfelCenterCoord + vec2(+0.0, -0.5);
    surfelCornerCoords.bottomRight = surfelCenterCoord + vec2(+0.5, -0.5);
    surfelCornerCoords.left        = surfelCenterCoord + vec2(-0.5, +0.0);
    surfelCornerCoords.center      = surfelCenterCoord + vec2(+0.0, +0.0);
    surfelCornerCoords.right       = surfelCenterCoord + vec2(+0.5, +0.0);
    surfelCornerCoords.topLeft     = surfelCenterCoord + vec2(-0.5, +0.5);
    surfelCornerCoords.top         = surfelCenterCoord + vec2(+0.0, +0.5);
    surfelCornerCoords.topRight    = surfelCenterCoord + vec2(+0.5, +0.5);

    // sample texture maps
    MultiQuad2D sampleCoords;
    sampleCoords.bottomLeft  = (pixelCenterCoords.bottomLeft  - vec2(epsilon)) / remoteWindowSize;
    sampleCoords.bottom      = (pixelCenterCoords.bottom      - vec2(epsilon)) / remoteWindowSize;
    sampleCoords.bottomRight = (pixelCenterCoords.bottomRight - vec2(epsilon)) / remoteWindowSize;
    sampleCoords.left        = (pixelCenterCoords.left        - vec2(epsilon)) / remoteWindowSize;
    sampleCoords.center      = (pixelCenterCoords.center      - vec2(epsilon)) / remoteWindowSize;
    sampleCoords.right       = (pixelCenterCoords.right       - vec2(epsilon)) / remoteWindowSize;
    sampleCoords.topLeft     = (pixelCenterCoords.topLeft     - vec2(epsilon)) / remoteWindowSize;
    sampleCoords.top         = (pixelCenterCoords.top         - vec2(epsilon)) / remoteWindowSize;
    sampleCoords.topRight    = (pixelCenterCoords.topRight    - vec2(epsilon)) / remoteWindowSize;

    MultiQuad1D pixelDepths;
    pixelDepths.bottomLeft  = sampleDepth(depthMap, sampleCoords.bottomLeft);
    pixelDepths.bottom      = sampleDepth(depthMap, sampleCoords.bottom);
    pixelDepths.bottomRight = sampleDepth(depthMap, sampleCoords.bottomRight);
    pixelDepths.left        = sampleDepth(depthMap, sampleCoords.left);
    pixelDepths.center      = sampleDepth(depthMap, sampleCoords.center);
    pixelDepths.right       = sampleDepth(depthMap, sampleCoords.right);
    pixelDepths.topLeft     = sampleDepth(depthMap, sampleCoords.topLeft);
    pixelDepths.top         = sampleDepth(depthMap, sampleCoords.top);
    pixelDepths.topRight    = sampleDepth(depthMap, sampleCoords.topRight);

    if (discardOutOfRangeDepths && pixelDepths.center >= MAX_DEPTH) {
        // store an empty plane to clear the output quad map
        QuadMapData ignoredQuadMapData;
        ignoredQuadMapData.size = 0;
        storeInQuadMap(pixelCoord, ignoredQuadMapData);
        return;
    }

    // transform normals to view space
    MultiQuad pixelNormalsView;
    pixelNormalsView.bottomLeft  = normalToView(view, sampleNormal(normalMap, sampleCoords.bottomLeft));
    pixelNormalsView.bottom      = normalToView(view, sampleNormal(normalMap, sampleCoords.bottom));
    pixelNormalsView.bottomRight = normalToView(view, sampleNormal(normalMap, sampleCoords.bottomRight));
    pixelNormalsView.left        = normalToView(view, sampleNormal(normalMap, sampleCoords.left));
    pixelNormalsView.center      = normalToView(view, sampleNormal(normalMap, sampleCoords.center));
    pixelNormalsView.right       = normalToView(view, sampleNormal(normalMap, sampleCoords.right));
    pixelNormalsView.topLeft     = normalToView(view, sampleNormal(normalMap, sampleCoords.topLeft));
    pixelNormalsView.top         = normalToView(view, sampleNormal(normalMap, sampleCoords.top));
    pixelNormalsView.topRight    = normalToView(view, sampleNormal(normalMap, sampleCoords.topRight));

    // project 2D points to 3D view space
    MultiQuad2D uvs;
    uvs.bottomLeft  = pixelCenterCoords.bottomLeft  / remoteWindowSize;
    uvs.bottom      = pixelCenterCoords.bottom      / remoteWindowSize;
    uvs.bottomRight = pixelCenterCoords.bottomRight / remoteWindowSize;
    uvs.left        = pixelCenterCoords.left        / remoteWindowSize;
    uvs.center      = pixelCenterCoords.center      / remoteWindowSize;
    uvs.right       = pixelCenterCoords.right       / remoteWindowSize;
    uvs.topLeft     = pixelCenterCoords.topLeft     / remoteWindowSize;
    uvs.top         = pixelCenterCoords.top         / remoteWindowSize;
    uvs.topRight    = pixelCenterCoords.topRight    / remoteWindowSize;

    MultiQuad2D pixelCenterNDCs;
    pixelCenterNDCs.bottomLeft  = uvs.bottomLeft  * 2.0 - 1.0;
    pixelCenterNDCs.bottom      = uvs.bottom      * 2.0 - 1.0;
    pixelCenterNDCs.bottomRight = uvs.bottomRight * 2.0 - 1.0;
    pixelCenterNDCs.left        = uvs.left        * 2.0 - 1.0;
    pixelCenterNDCs.center      = uvs.center      * 2.0 - 1.0;
    pixelCenterNDCs.right       = uvs.right       * 2.0 - 1.0;
    pixelCenterNDCs.topLeft     = uvs.topLeft     * 2.0 - 1.0;
    pixelCenterNDCs.top         = uvs.top         * 2.0 - 1.0;
    pixelCenterNDCs.topRight    = uvs.topRight    * 2.0 - 1.0;

    MultiQuad2D surfelNDCs;
    surfelNDCs.bottomLeft  = (surfelCornerCoords.bottomLeft  / remoteWindowSize) * 2.0 - 1.0;
    surfelNDCs.bottom      = (surfelCornerCoords.bottom      / remoteWindowSize) * 2.0 - 1.0;
    surfelNDCs.bottomRight = (surfelCornerCoords.bottomRight / remoteWindowSize) * 2.0 - 1.0;
    surfelNDCs.left        = (surfelCornerCoords.left        / remoteWindowSize) * 2.0 - 1.0;
    surfelNDCs.center      = (surfelCornerCoords.center      / remoteWindowSize) * 2.0 - 1.0;
    surfelNDCs.right       = (surfelCornerCoords.right       / remoteWindowSize) * 2.0 - 1.0;
    surfelNDCs.topLeft     = (surfelCornerCoords.topLeft     / remoteWindowSize) * 2.0 - 1.0;
    surfelNDCs.top         = (surfelCornerCoords.top         / remoteWindowSize) * 2.0 - 1.0;
    surfelNDCs.topRight    = (surfelCornerCoords.topRight    / remoteWindowSize) * 2.0 - 1.0;

    MultiQuad pixelCoordsView;
    pixelCoordsView.bottomLeft  = ndcToView(projectionInverse, pixelCenterNDCs.bottomLeft, pixelDepths.bottomLeft);
    pixelCoordsView.bottom      = ndcToView(projectionInverse, pixelCenterNDCs.bottom, pixelDepths.bottom);
    pixelCoordsView.bottomRight = ndcToView(projectionInverse, pixelCenterNDCs.bottomRight, pixelDepths.bottomRight);
    pixelCoordsView.left        = ndcToView(projectionInverse, pixelCenterNDCs.left, pixelDepths.left);
    pixelCoordsView.center      = ndcToView(projectionInverse, pixelCenterNDCs.center, pixelDepths.center);
    pixelCoordsView.right       = ndcToView(projectionInverse, pixelCenterNDCs.right, pixelDepths.right);
    pixelCoordsView.topLeft     = ndcToView(projectionInverse, pixelCenterNDCs.topLeft, pixelDepths.topLeft);
    pixelCoordsView.top         = ndcToView(projectionInverse, pixelCenterNDCs.top, pixelDepths.top);
    pixelCoordsView.topRight    = ndcToView(projectionInverse, pixelCenterNDCs.topRight, pixelDepths.topRight);

    if (doAverageNormal) {
        pixelNormalsView.center = averageNormals(pixelNormalsView, pixelCoordsView);
    }

    // project all points to near plane
    MultiQuad surfelViewNearPlane;
    surfelViewNearPlane.bottomLeft  = ndcToView(projectionInverse, surfelNDCs.bottomLeft, 0.0);
    surfelViewNearPlane.bottom      = ndcToView(projectionInverse, surfelNDCs.bottom, 0.0);
    surfelViewNearPlane.bottomRight = ndcToView(projectionInverse, surfelNDCs.bottomRight, 0.0);
    surfelViewNearPlane.left        = ndcToView(projectionInverse, surfelNDCs.left, 0.0);
    surfelViewNearPlane.center      = ndcToView(projectionInverse, surfelNDCs.center, 0.0);
    surfelViewNearPlane.right       = ndcToView(projectionInverse, surfelNDCs.right, 0.0);
    surfelViewNearPlane.topLeft     = ndcToView(projectionInverse, surfelNDCs.topLeft, 0.0);
    surfelViewNearPlane.top         = ndcToView(projectionInverse, surfelNDCs.top, 0.0);
    surfelViewNearPlane.topRight    = ndcToView(projectionInverse, surfelNDCs.topRight, 0.0);

    // create surfels
    MultiQuad surfelView;
    createSurfels(surfelView, surfelNDCs, surfelViewNearPlane, pixelCoordsView, pixelNormalsView, pixelDepths);

    vec3 frustumNormalView = normalize(origin - surfelView.center);
    if (doOrientationCorrection && quadHasExtremeOrientation(surfelView, frustumNormalView)) {
        // recreate surfels with rotated normal
        pixelNormalsView.center = constrainToMaxAngle(pixelNormalsView.center, frustumNormalView, angleThreshold);
        createSurfels(surfelView, surfelNDCs, surfelViewNearPlane, pixelCoordsView, pixelNormalsView, pixelDepths);
    }

    /**********************

        stl -  st - str
        |      |     |
       (sl) - (sc) - sr
        |      |     |
      (sbl) - (sb) - sbr

    **********************/
    Quad subQuadBottomLeft;
    subQuadBottomLeft.topLeft = surfelView.left;
    subQuadBottomLeft.topRight = surfelView.center;
    subQuadBottomLeft.bottomLeft = surfelView.bottomLeft;
    subQuadBottomLeft.bottomRight = surfelView.bottom;

    /**********************

        stl -  st - str
        |      |     |
        sl - (sc) - (sr)
        |      |     |
       sbl - (sb) - (sbr)

    **********************/
    Quad subQuadBottomRight;
    subQuadBottomRight.topLeft = surfelView.center;
    subQuadBottomRight.topRight = surfelView.right;
    subQuadBottomRight.bottomLeft = surfelView.bottom;
    subQuadBottomRight.bottomRight = surfelView.bottomRight;

    /**********************

     (stl) - (st) - str
        |     |     |
      (sl) - (sc) - sr
        |     |     |
       sbl - sb - sbr

    **********************/
    Quad subQuadTopLeft;
    subQuadTopLeft.topLeft = surfelView.topLeft;
    subQuadTopLeft.topRight = surfelView.top;
    subQuadTopLeft.bottomLeft = surfelView.left;
    subQuadTopLeft.bottomRight = surfelView.center;

    /**********************

       stl - (st) - (str)
        |     |       |
        sl - (sc) - (sr)
        |     |       |
       sbl -  sb  -  sbr

    **********************/
    Quad subQuadTopRight;
    subQuadTopRight.topLeft = surfelView.top;
    subQuadTopRight.topRight = surfelView.topRight;
    subQuadTopRight.bottomLeft = surfelView.center;
    subQuadTopRight.bottomRight = surfelView.right;

    // save quads in quad map
    storeSurfelsInQuadMap(
        pixelCoord,
        pixelCenterCoords.center,
        pixelNormalsView.center,
        subQuadBottomLeft, subQuadBottomRight, subQuadTopLeft, subQuadTopRight
    );
}
