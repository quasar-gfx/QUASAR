layout(local_size_x = THREADS_PER_LOCALGROUP, local_size_y = THREADS_PER_LOCALGROUP, local_size_z = 1) in;

layout(binding = 0) uniform sampler2D depthMap;

struct Vertex {
    uint id;
    vec3 position;
    vec3 color;
    vec3 normal;
    vec2 texCoords;
    vec3 tangent;
    vec3 bitangent;
};

layout(std430, binding = 0) buffer VertexBuffer {
    Vertex vertices[];
};

layout(std430, binding = 1) buffer IndexBuffer {
    uint indices[];
};

uniform vec2 screenSize;

uniform mat4 viewColor;
uniform mat4 viewInverseDepth;
uniform mat4 projection;
uniform mat4 projectionInverse;

uniform float near;
uniform float far;

uniform int surfelSize;

#define VERTICES_IN_A_QUAD  4
#define INDICES_IN_A_QUAD   6

vec3 ndcToView(mat4 projectionInverse, vec2 ndc, float depth) {
    vec4 ndcPos;
    ndcPos.xy = ndc;
    ndcPos.z = 2.0 * depth - 1.0;
    ndcPos.w = 1.0;

    vec4 viewCoord = projectionInverse * ndcPos;
    viewCoord = viewCoord / viewCoord.w;
    return viewCoord.xyz;
}

vec3 viewToWorld(mat4 viewInverse, vec3 viewCoord) {
    vec4 worldCoord = viewInverse * vec4(viewCoord, 1.0);
    worldCoord = worldCoord / worldCoord.w;
    return worldCoord.xyz;
}

vec3 worldToView(mat4 view, vec3 worldCoord) {
    vec4 viewCoord = view * vec4(worldCoord, 1.0);
    viewCoord = viewCoord / viewCoord.w;
    return viewCoord.xyz;
}

vec2 viewToNDC(mat4 projection, vec3 viewCoord) {
    vec4 ndcCoord = projection * vec4(viewCoord, 1.0);
    ndcCoord = ndcCoord / ndcCoord.w;
    return ndcCoord.xy;
}

vec2 viewToScreen(mat4 projection, vec3 viewCoord) {
    vec2 ndc = viewToNDC(projection, viewCoord).xy;
    vec2 uv = (ndc + 1.0) / 2.0;
    return uv;
}

float LinearizeDepth(float depth) {
    float z = depth * 2.0 - 1.0; // back to NDC
    return (2.0 * near * far) / (far + near - z * (far - near));
}

float UnLinearizeDepth(float depth) {
    depth *= far;
    depth = max(depth, near);
    float z = (depth * (far + near) - 2.0 * near * far) / (depth * (far - near));
    return (z + 1.0) / 2.0;
}

float sampleDepth(sampler2D depthMap, vec2 sampleCoord) {
    float depth = texture(depthMap, sampleCoord).r;
    depth = UnLinearizeDepth(depth);
    depth = clamp(depth, 0.0, 0.99999);
    return depth;
}

void main() {
    vec2 size = screenSize / float(surfelSize);

    vec2 surfelCoord = vec2(gl_GlobalInvocationID.xy);
    if (surfelCoord.x >= screenSize.x || surfelCoord.y >= screenSize.y) {
        return;
    }

    vec2 pixelCoord = surfelSize * (surfelCoord + vec2(0.5, 0.5));
    vec2 uv = vec2(pixelCoord) / screenSize;

    float depth = sampleDepth(depthMap, uv);

    vec2 ndc = 2.0 * uv - 1.0;
    vec3 viewCoord = ndcToView(projectionInverse, ndc, depth);
    vec3 worldCoord = viewToWorld(viewInverseDepth, viewCoord);

    uint shaderIndex = uint(gl_GlobalInvocationID.y * size.x + gl_GlobalInvocationID.x);
    vertices[shaderIndex].position = worldCoord;
    vertices[shaderIndex].color = vec3(1.0, 1.0, 1.0);
    vertices[shaderIndex].texCoords = viewToScreen(projection, viewCoord);

    if (pixelCoord.x < screenSize.x - surfelSize && pixelCoord.y < screenSize.y - surfelSize) {
        /************************

                tl -- tr
                |    / |
                |   c  |
                |  /   |
                bl -- br

        ************************/
        uint bottomLeftIdx = shaderIndex;
        uint bottomRightIdx = shaderIndex + 1;
        uint topLeftIdx = shaderIndex + uint(size.x);
        uint topRightIdx = shaderIndex + uint(size.x) + 1;

        indices[shaderIndex * INDICES_IN_A_QUAD + 0] = topLeftIdx;
        indices[shaderIndex * INDICES_IN_A_QUAD + 1] = bottomLeftIdx;
        indices[shaderIndex * INDICES_IN_A_QUAD + 2] = topRightIdx;

        indices[shaderIndex * INDICES_IN_A_QUAD + 3] = topRightIdx;
        indices[shaderIndex * INDICES_IN_A_QUAD + 4] = bottomLeftIdx;
        indices[shaderIndex * INDICES_IN_A_QUAD + 5] = bottomRightIdx;
    }
}
